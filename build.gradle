plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.graalvm.buildtools.native' version '0.9.20'
}

group = 'it.gov.pagopa'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
	maven {
		url "https://www.gridgainsystems.com/nexus/content/repositories/external"
	}
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

def gridgainVersion = '8.8.27'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

	// ignite (aka gridgain community edition)
	//implementation "org.gridgain:gridgain-bom:$gridgainVersion"
	implementation "org.gridgain:ignite-core:$gridgainVersion"
	implementation "org.gridgain:ignite-spring:$gridgainVersion"
	implementation "org.gridgain:ignite-indexing:$gridgainVersion"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

// https://ignite.apache.org/docs/latest/quick-start/java#running-ignite-with-java-11-or-later
tasks.withType(JavaExec).configureEach {
	jvmArgs = [
			"-Xms512m",
			"-Xmx768m",
			"--add-opens=jdk.management/com.sun.management.internal=ALL-UNNAMED",
			"--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED",
			"--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
			"--add-opens=java.management/com.sun.jmx.mbeanserver=ALL-UNNAMED",
			"--add-opens=jdk.internal.jvmstat/sun.jvmstat.monitor=ALL-UNNAMED",
			"--add-opens=java.base/sun.reflect.generics.reflectiveObjects=ALL-UNNAMED",
			"--add-opens=java.base/java.io=ALL-UNNAMED",
			"--add-opens=java.base/java.nio=ALL-UNNAMED",
			"--add-opens=java.base/java.util=ALL-UNNAMED",
			"--add-opens=java.base/java.lang=ALL-UNNAMED"
	]
}